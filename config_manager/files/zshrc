# git clone https://github.com/zsh-users/zsh-autosuggestions ~/.config/zsh/plugins/zsh-autosuggestions
# git clone https://github.com/zsh-users/zsh-syntax-highlighting ~/.config/zsh/plugins/zsh-syntax-highlighting

# autosuggestion and syntax highlighting
source ~/.config/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
source ~/.config/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

source $(brew --prefix)/opt/fzf/shell/key-bindings.zsh
source $(brew --prefix)/opt/fzf/shell/completion.zsh

# Completion
autoload -Uz compinit && compinit

# MenuSelect
zmodload -i zsh/complist

# vim mode
bindkey -v
export KEYTIMEOUT=1

# Emacs bindings in insert mode
bindkey '^A' beginning-of-line
bindkey '^E' end-of-line
bindkey '^R' history-incremental-search-backward
bindkey '^P' up-line-or-history
bindkey '^N' down-line-or-history

# Auto-completion menu select
zstyle ':completion:*' menu select
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}' 

# vim navigation for menu select
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# History
HISTSIZE=20000
SAVEHIST=20000
HISTFILE=~/.zsh_history
setopt histignorealldups sharehistory

# Custom function to search history using fzf and exclude line numbers
fzf_history_search() {
    local selected_command
    # Extract history without numbers, pass to fzf, and capture the selected command
    selected_command=$(fc -rl 1 | sed 's/^[[:space:]]*[0-9]*[[:space:]]*//' | fzf --height 40% --reverse --border --inline-info)

    # If a command is selected, place it into the terminal
    if [[ -n $selected_command ]]; then
        BUFFER="$selected_command"
        CURSOR=${#BUFFER}
    fi

    zle redisplay
}

# search grep
sg() {
    rg --line-number --no-heading --color=always --smart-case ${1:-""} | \
    fzf --height 40% \
        --ansi \
        --delimiter : \
        --preview 'bat --style=numbers --color=always {1} --highlight-line {2}' \
        --preview-window 'right:70%:wrap' \
        --bind 'ctrl-d:preview-half-page-down,ctrl-u:preview-half-page-up' \
        --bind 'ctrl-j:preview-down,ctrl-k:preview-up' \
        --bind 'enter:execute(nvim {1} +{2} < /dev/tty)' \
        --bind 'ctrl-space:execute(code {1} --goto {1}:{2})'
}

fv() {
fzf --preview 'bat --style=numbers --color=always {}' \
    --preview-window 'right:70%:wrap' \
    --bind 'alt-j:down,alt-k:up' \
    --bind 'ctrl-d:preview-half-page-down,ctrl-u:preview-half-page-up' \
    --bind 'ctrl-j:preview-down,ctrl-k:preview-up' \
    --bind 'enter:execute(nvim {} < /dev/tty)' \
    --bind 'ctrl-space:execute(code {})'
}

# Bind `/` to fzf history search in Vim's normal mode
zle -N fzf_history_search
bindkey -M vicmd '/' fzf_history_search

export MANPAGER="sh -c 'col -bx | bat --paging=always --language=man'"
eval "$(zoxide init zsh)"
eval "$(starship init zsh)"
